# Maintainet: Hayate Nakamura <is01.njb at gmail dot com>
# Contributor: e5ten <e5ten.arch@gmail.com>
# Contributor: sxe <sxxe@gmx.de>
pkgname='qlipper'
pkgver=5.1.2
pkgrel=1.2univalent1
pkgdesc='Lightweight & cross-platform clipboard history applet based on qt'
arch=('i686' 'x86_64')
url="https://github.com/pvanek/${pkgname}"
license=('GPL2')
depends=('qt5-base')
makedepends=('cmake' 'ninja' 'qt5-tools' 'git')
source=("git+https://github.com/pvanek/${pkgname}#tag=${pkgver}"
	"lxqt-qlipper-autostart.desktop")
sha256sums=('SKIP'
	'de197c66b4ac926d138150c397b97993b9ab675a205761c5616c934b15cdcaf6')

build() {
    cmake -G 'Ninja' -B "${srcdir}/build" \
		-DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCMAKE_BUILD_TYPE:STRING='Release' \
		"${srcdir}/${pkgname}"
    cmake --build "${srcdir}/build"
}

package() {
    DESTDIR="${pkgdir}" cmake --build "${srcdir}/build" --target install
    install -Dm755 "${srcdir}/lxqt-qlipper-autostart.desktop" "$pkgdir/etc/xdg/autostart/lxqt-qlipper-autostart.desktop"
}

