# Maintainer: Hayate Nakamura <is01.njb at gmail dot com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy-unicode
_pkgname=ibus-anthy
pkgver=1.5.14
pkgrel=2
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('x86_64')
license=('LGPL')
url='https://github.com/ibus/ibus-anthy'
conflicts=('ibus-anthy')
depends=('ibus' 'anthy-unicode' 'python' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('intltool' 'gobject-introspection')
source=("https://github.com/ibus/ibus-anthy/archive/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('4d06d4e177df62d22c61bf6ebfda04ddbf545ea1947d0c315e3dfe5e48d9ddc57e5fcc89919ae3b9f7430e6124a5dfb1295f4ca7362fbc35714d627f80fae6d8')

prepare() {
  cd ${_pkgname}-${pkgver}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${_pkgname}-${pkgver}

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
