# Maintainer: Hayate Nakamura <is01.njb at gmail dot com>

pkgname=kasumi-unicode-git
_pkgname=kasumi-unicode
pkgver=r162.e7bf93c
pkgrel=1
pkgdesc='Dictionary management tool for anthy-unicode'
arch=('x86_64')
url='https://github.com/fujiwarat/kasumi-unicode'
license=('LGPL' 'GPL')
source=("${_pkgname}::git+${url}.git")
sha512sums=('SKIP')
depends=('anthy-unicode' 'gtk3')
makedepends=('gcc11' 'git')
provides=('kasumi-unicode')
conflicts=('kasumi')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	export CXXFLAGS='-std=c++11'
	cd "${srcdir}/${_pkgname}"
 ./autogen.sh --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make DESTDIR="${pkgdir}" install
}
