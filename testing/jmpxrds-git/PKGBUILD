# Maintainer: Hayate Nakamura <hayatehay.njb at gmail dot com>
pkgname=jmpxrds-git
_pkgname=jmpxrds
pkgver=r162.0fe0847
pkgrel=1
pkgdesc="FM MPX signal generator with RDS support on top of Jack Audio Connection Kit"
arch=('x86_64')
url="https://github.com/uoc-radio/jmpxrds"
license=('GPL3')
depends=('gtk3' 'jack' 'flac')
makedepends=('git' 'autoconf')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=(${_pkgname}::"git+${url}.git")
md5sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}/"
  autoreconf -ivf
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_pkgname}/"
  make DESTDIR="$pkgdir" install
}
