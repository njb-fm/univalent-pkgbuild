# Maintainer: Hayate Nakamura <is01.njb at gmail dot com>

pkgname=anthy-unicode-nk
_pkgname=anthy-unicode
pkgver=1.0.0.20211224
pkgrel=1
pkgdesc='Hiragana text to Kana Kanji mixed text Japanese input method'
arch=('x86_64')
url='https://github.com/fujiwarat/anthy-unicode'
license=('LGPL' 'GPL' 'Apache' 'custom')
source=("${url}/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        "makefile-am.patch"
        "makefile-in.patch"
        "git+https://github.com/jin-asanami/anthydic-nk.git#commit=1d693d87939a64608d136542f0bd8952ccdc7a84"
        "https://github.com/neologd/mecab-ipadic-neologd/raw/master/seed/mecab-user-dict-seed.20200910.csv.xz"
        "https://www.post.japanpost.jp/zipcode/dl/kogaki/zip/ken_all.zip"
        "http://sudachi.s3-website-ap-northeast-1.amazonaws.com/sudachidict-raw/20230110/core_lex.zip"
        "http://sudachi.s3-website-ap-northeast-1.amazonaws.com/sudachidict-raw/20230110/notcore_lex.zip")
noextract=("mecab-user-dict-seed.20200910.csv.xz"
           "ken_all.zip"
           "core_lex.zip"
           "notcore_lex.zip")
sha512sums=('8971129df716b4cb21c5c9fb24a98388381d8172004126f68c037d47721b0fd9ab9cb0b6ef73414e62bf9d832c59f09743546da72366c7692bfd146a05bf800d'
            '8cffb9e78ddaa15a9b1aa330ded9970fe3b94859edb2e92c0c506db5904a7043151ebe594dfd891eb055c3e55fff3fb7bd1afb5497ea10d025de81591ab1a963'
            '80cdf31b07a2d3bb13bbf4630a25632ac5c265d6a60041f531f99e6fcc5c875c3a39d782bde4758d013a4e6d3fe8111d7e64bcb05517f0ab145d33c72bd63f1a'
            'SKIP'
            '391bf23f4163f5d40abef49ac7ee3e856a0f06f83adde13c5709e86480be93d4087ca72d244dd57a8bf45e881958a96f59b55c695671aa59a3eb15532ecb9ce1'
            'da58eb09ffb56eb463b584b51f65f3569336c5ba4a43f1c2e2014b9a0404637c74e2ffb54efad641a912617c1021cc2541b3fe8abc3dd8d94affae6355960363'
            '46de5c238cdfe39e28d29cff317bdab1ad26a1f6999141efbcc443cb8269d714cbebf8ef9582d53b2ee722c2511d9d0735a67bdce345ad5a15d63ef42d2f5745'
            '53f319879a7bdd8b6d6a527901e8a25079a023799aa06b786aee67745739b7f81d157b2f065b4a4738ca9ec01519ab9f11be81565f1837de5df8010b3f345821')
makedepends=('emacs' 'ruby' 'nkf' 'unzip')
provides=('anthy' 'anthy-unicode')
conflicts=('anthy' 'anthy-unicode')

prepare() {
	# Patch
	cd ${srcdir}/${_pkgname}-${pkgver}
	patch -p1 -i ../makefile-am.patch
	patch -p1 -i ../makefile-in.patch

	# Create a directory for additional dictionaries
	cd ${srcdir}
	mkdir ${_pkgname}-${pkgver}/anthydic-nk

	# Copy compressed files
	cp mecab-user-dict-seed.20200910.csv.xz ${srcdir}/anthydic-nk/src/free/neologd/
	cp ken_all.zip ${srcdir}/anthydic-nk/src/free/place-names/
	cp core_lex.zip ${srcdir}/anthydic-nk/src/free/sudachidict/
	cp notcore_lex.zip ${srcdir}/anthydic-nk/src/free/sudachidict/
	sed "s/wget/#wget/g" -i ${srcdir}/anthydic-nk/src/free/*/makedic.sh

	# Build NEologd dict
	cd ${srcdir}/anthydic-nk/src/free/neologd
	./makedic.sh

	# Build place names dict
	cd ${srcdir}/anthydic-nk/src/free/place-names
	./makedic.sh

	# Build SudachiDict
	cd ${srcdir}/anthydic-nk/src/free/sudachidict
	./makedic.sh

	# Move dictonaries
	cd ${srcdir}
	cp anthydic-nk/src/free/neologd/neologd.t ${_pkgname}-${pkgver}/anthydic-nk/
	cp anthydic-nk/src/free/place-names/place-names.t ${_pkgname}-${pkgver}/anthydic-nk/
	cp anthydic-nk/src/free/sudachidict/sudachidict.t ${_pkgname}-${pkgver}/anthydic-nk/
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make EMACS=emacs DESTDIR="${pkgdir}" install

	cd ${pkgdir}/etc
	ln -sf anthy-unicode.conf anthy-conf

	cd ${pkgdir}/usr/bin
	ln -sf anthy-agent-unicode anthy-agent
	ln -sf anthy-dic-tool-unicode anthy-dic-tool
	ln -sf anthy-morphological-analyzer-unicode anthy-morphological-analyzer

	cd ${pkgdir}/usr/include
	ln -sf anthy-unicode-1.0/anthy anthy

	cd ${pkgdir}/usr/lib
	ln -sf libanthy-unicode.so.0.1.0 libanthy.so.0.1.0
	ln -sf libanthy-unicode.so.0.1.0 libanthy.so.0
	ln -sf libanthy-unicode.so.0.1.0 libanthy.so
	ln -sf libanthydic-unicode.so.0.1.0 libanthydic.so.0.1.0
	ln -sf libanthydic-unicode.so.0.1.0 libanthydic.so.0
	ln -sf libanthydic-unicode.so.0.1.0 libanthydic.so
	ln -sf libanthyinput-unicode.so.0.0.0 libanthyinput.so.0.0.0
	ln -sf libanthyinput-unicode.so.0.0.0 libanthyinput.so.0
	ln -sf libanthyinput-unicode.so.0.0.0 libanthyinput.so

	cd ${pkgdir}/usr/share
	ln -sf anthy-unicode anthy

	mkdir ${pkgdir}/usr/share/emacs/site-lisp/anthy
	cd ${pkgdir}/usr/share/emacs/site-lisp/anthy
	ln -sf ../anthy-unicode/anthy-unicode-azik.el anthy-azik.el
	ln -sf ../anthy-unicode/anthy-unicode-conf.el anthy-conf.el
	ln -sf ../anthy-unicode/anthy-unicode-dic.el anthy-dic.el
	ln -sf ../anthy-unicode/anthy-unicode-isearch.el anthy-isearch.el
	ln -sf ../anthy-unicode/anthy-unicode-kyuri.el anthy-kyuri.el
	ln -sf ../anthy-unicode/anthy-unicode.el anthy.el
	ln -sf ../anthy-unicode/leim-list.el leim-list.el
}
