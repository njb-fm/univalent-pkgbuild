# Maintainer: Hayate Naka-mura <is01.njb at gmail dot com>

pkgname=anthy-nk
_pkgname=anthy
_pkgver=9100h
_nkdicver=1d693d8
pkgver=${_pkgver}.nk${_nkdicver}
_filecode=37536
pkgrel=6
pkgdesc='Hiragana text to Kana Kanji mixed text Japanese input method'
arch=('x86_64')
url='https://es.osdn.net/projects/anthy/'
license=('LGPL' 'GPL' 'Apache' 'custom')
source=("https://ftp.jaist.ac.jp/pub/sourceforge.jp/anthy/${_filecode}/${_pkgname}-${_pkgver}.tar.gz"
        "git+https://github.com/jin-asanami/anthydic-nk.git#commit=1d693d87939a64608d136542f0bd8952ccdc7a84"
        "https://github.com/neologd/mecab-ipadic-neologd/raw/master/seed/mecab-user-dict-seed.20200910.csv.xz"
        "https://www.post.japanpost.jp/zipcode/dl/kogaki/zip/ken_all.zip"
        "http://sudachi.s3-website-ap-northeast-1.amazonaws.com/sudachidict-raw/20230110/core_lex.zip"
        "http://sudachi.s3-website-ap-northeast-1.amazonaws.com/sudachidict-raw/20230110/notcore_lex.zip")
noextract=("mecab-user-dict-seed.20200910.csv.xz"
           "ken_all.zip"
           "core_lex.zip"
           "notcore_lex.zip")
sha512sums=('315ebd9e1af208f3ecaeaa13620213b35f004e47edb54e60c3e02c2997444cd2326cc58b725dd2d0bd6a723130884d946ea193ea3d92418082256b59ecc8d88b'
            'SKIP'
            '391bf23f4163f5d40abef49ac7ee3e856a0f06f83adde13c5709e86480be93d4087ca72d244dd57a8bf45e881958a96f59b55c695671aa59a3eb15532ecb9ce1'
            'da58eb09ffb56eb463b584b51f65f3569336c5ba4a43f1c2e2014b9a0404637c74e2ffb54efad641a912617c1021cc2541b3fe8abc3dd8d94affae6355960363'
            '46de5c238cdfe39e28d29cff317bdab1ad26a1f6999141efbcc443cb8269d714cbebf8ef9582d53b2ee722c2511d9d0735a67bdce345ad5a15d63ef42d2f5745'
            '53f319879a7bdd8b6d6a527901e8a25079a023799aa06b786aee67745739b7f81d157b2f065b4a4738ca9ec01519ab9f11be81565f1837de5df8010b3f345821')
makedepends=('emacs' 'ruby' 'unzip' 'p7zip')
provides=('anthy')
conflicts=('anthy')


prepare() {
	# Copy compressed files
	cd ${srcdir}
	cp ../mecab-user-dict-seed.20200910.csv.xz anthydic-nk/src/free/neologd/
	cp ../ken_all.zip anthydic-nk/src/free/place-names/
	cp ../core_lex.zip anthydic-nk/src/free/sudachidict/
	cp ../notcore_lex.zip anthydic-nk/src/free/sudachidict/
	sed "s/wget/#wget/g" -i ${srcdir}/anthydic-nk/src/free/*/makedic.sh

	# Build NEologd dict
	cd ${srcdir}/anthydic-nk/src/free/neologd
	./makedic.sh

	# Build place names dict
	cd ${srcdir}/anthydic-nk/src/free/place-names
	./makedic.sh

	# Build SudachiDict
	cd ${srcdir}/anthydic-nk/src/free/sudachidict
	./makedic.sh

	# Merge dictonaries
	cd ${srcdir}
	cat anthydic-nk/src/free/neologd/neologd.t >> ${_pkgname}-${_pkgver}/mkworddic/base.t
	cat anthydic-nk/src/free/place-names/place-names.t >> ${_pkgname}-${_pkgver}/mkworddic/base.t
	cat anthydic-nk/src/free/sudachidict/sudachidict.t >> ${_pkgname}-${_pkgver}/mkworddic/base.t
}

build() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"
	make EMACS=emacs DESTDIR="${pkgdir}" install
}
